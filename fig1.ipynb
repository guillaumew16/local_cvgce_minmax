{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# import Pkg\n",
    "# Pkg.add(\"LinearRegression\")\n",
    "# Pkg.add(\"JLD\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "rng = MersenneTwister(1234)\n",
    "import Dates\n",
    "using JLD\n",
    "import LinearAlgebra as LA\n",
    "import LinearRegression as LinReg"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot\n",
    "using Colors\n",
    "using LaTeXStrings"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function eta_opt_simGDA(M)\n",
    "    return minimum(real.(1 ./ LA.eigvals(M)))  # Gidel et al. '19 \"Negative momentum...\" [Thm 2]\n",
    "end\n",
    "\n",
    "function eta_opt_altGDA(M, eta0, cnt_max=10)\n",
    "    A = (M-M')/2\n",
    "    dim = size(M)[1]\n",
    "    nablaT_alt(eta) = LA.I(dim) .- eta * M .+ (eta^2/2) * A * M  # actually an approximation\n",
    "    rho(eta) = maximum(abs.(LA.eigvals(nablaT_alt(eta))))\n",
    "    eta = eta0\n",
    "    cnt = 0\n",
    "    while rho(eta) >= 1 && (cnt+=1)<cnt_max\n",
    "        eta = eta/2\n",
    "    end\n",
    "    if (cnt == cnt_max)   @warn(\"cnt == cnt_max\")  end\n",
    "    return eta\n",
    "end\n",
    "\n",
    "minRe_exact(M) = minimum(real.(LA.eigvals(M)))\n",
    "\n",
    "function rho_altGDA(M, eta)\n",
    "    A = (M-M')/2\n",
    "    dim = size(M)[1]\n",
    "    nablaT = LA.I(dim) .- eta * M .+ (eta^2/2) * A * M\n",
    "    return maximum(abs.(LA.eigvals(nablaT)))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Random.seed!(rng, 1234)\n",
    "\n",
    "dimx, dimy = 2, 2  # (need to be equal for the NE to be unique)\n",
    "alpha_s = 10. .^ (-3:0.25:1)\n",
    "N = 10\n",
    "T_max = Int(4e7)\n",
    "T0 = 8000  # must be large enough to capture the regime where the dynamics is driven by the least eigenvalue, with all other eigenspaces fully fitted\n",
    "etamult = 0.1  # safety margin to ensure small-stepsize regime\n",
    "P_s = zeros(dimx, dimy, N)\n",
    "eta_s = zeros(length(alpha_s), N)\n",
    "rate_s, linreg_relerr_s = zeros(length(alpha_s), N), zeros(length(alpha_s), N)\n",
    "minRe_s, rho_s = zeros(length(alpha_s), N), zeros(length(alpha_s), N)\n",
    "\n",
    "for n=1:N\n",
    "    P = randn(rng, dimx, dimy)\n",
    "    P_s[:,:,n] = P\n",
    "    x0, y0 = randn(rng, dimx) .* 1e-3, randn(rng, dimy) .* 1e-3\n",
    "    M = zeros(dimx+dimy, dimx+dimy)\n",
    "    M[1:dimx, dimx+1:end] = P\n",
    "    M[dimx+1:end, 1:dimx] = -P'\n",
    "\n",
    "    for (i, alpha) in enumerate(alpha_s)\n",
    "        if alpha <= 4e-3   T0 = 12000 else T0 = 8000 end\n",
    "\n",
    "        M[1,1] = alpha\n",
    "        eta_opt = eta_opt_altGDA(M, 1/LA.opnorm(P))\n",
    "        eta = etamult * eta_opt\n",
    "        eta_s[i,n] = eta\n",
    "\n",
    "        minRe_s[i, n] = minRe_exact(M)\n",
    "        rho_s[i,n] = rho_altGDA(M, eta)\n",
    "\n",
    "        T = min(T_max, Int(floor(T0/eta)))\n",
    "        dists = zeros(T+1)\n",
    "\n",
    "        # ## Simultaneous GDA\n",
    "        # z = vcat(x0, y0)\n",
    "        # for t=1:T\n",
    "        #     dists[t] = sum(z .^ 2)\n",
    "        #     z = z .- eta .* M * z\n",
    "        # end\n",
    "        # dists[T+1] = sum(z .^ 2)\n",
    "\n",
    "        ## Alternating GDA\n",
    "        x, y = x0, y0\n",
    "        for t=1:T\n",
    "            dists[t] = sqrt(sum(x .^ 2)) + sqrt(sum(y .^ 2))\n",
    "            # dists[t] = sum(x .^ 2) + sum(y .^ 2)\n",
    "            x1prev = x[1]\n",
    "            x = x .- eta .* P * y\n",
    "            x[1] = x[1] - eta * alpha * x1prev\n",
    "            y = y .+ eta .* P' * x\n",
    "        end\n",
    "        dists[T+1] = sqrt(sum(x .^ 2)) + sqrt(sum(y .^ 2))\n",
    "        # dists[T+1] = sum(x .^ 2) + sum(y .^ 2)\n",
    "        # dists = sqrt.(dists)\n",
    "\n",
    "        T_machineprecision = findfirst(.<(5e-16), dists)\n",
    "        T = isnothing(T_machineprecision) ? T : T_machineprecision\n",
    "        T_burnin = Int(floor(3*T/4))\n",
    "        T_rg = collect(T_burnin:1:T)\n",
    "        slope, intercept = LinReg.coef(LinReg.linregress(T_rg, log.(dists[T_rg])))\n",
    "        rate_s[i,n] = -slope\n",
    "        linreg_err = sum(( intercept .+ T_rg * slope .- log.(dists[T_rg]) ) .^ 2) / length(T_rg)\n",
    "        linreg_relerr_s[i,n] = sqrt(linreg_err) / sum(log.(dists[T_rg])) * length(T_rg)\n",
    "    end\n",
    "end\n",
    "rate_normalized_s = (1 .- exp.(-rate_s)) ./ eta_s\n",
    "linreg_relerr_s"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "save(\"reg_bilin_data.jld\", \n",
    "    \"alpha_s\", alpha_s, \n",
    "    \"P_s\", P_s,\n",
    "    \"eta_s\", eta_s,\n",
    "    \"rate_s\", rate_s, \n",
    "    \"linreg_relerr_s\", linreg_relerr_s, \n",
    "    \"minRe_s\", minRe_s, \n",
    "    \"rho_s\", rho_s)\n",
    "\n",
    "# d = load(\"reg_bilin_data.jld\")\n",
    "# alpha_s = d[\"alpha_s\"]\n",
    "# eta_s   = d[\"eta_s\"]\n",
    "# rate_s  = d[\"rate_s\"]\n",
    "# minRe_s = d[\"minRe_s\"]\n",
    "# rho_s   = d[\"rho_s\"]\n",
    "# rate_normalized_s = (1 .- exp.(-rate_s)) ./ eta_s"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "cm = get_cmap(:tab20)\n",
    "colorrange = (0:19) ./ 20\n",
    "figure(figsize=[5,3])\n",
    "for n=1:N\n",
    "    loglog(alpha_s, rate_normalized_s[:,n], lw=3, \n",
    "        label=(n==1 ? L\"$r/\\eta$ (observed)\" : \"\"),\n",
    "        \"o\", color=cm(colorrange[n]))\n",
    "    loglog(alpha_s, minRe_s[:,n], lw=1, \n",
    "        label=(n==1 ? L\"$\\tilde{\\mu}_M$\" : \"\"),\n",
    "        color=cm(colorrange[n]))\n",
    "end\n",
    "legend()\n",
    "xlabel(L\"\\alpha\")\n",
    "grid(\"on\")\n",
    "savefig(\"reg_bilin_rates__dim$(dimx)$(dimy).png\", bbox_inches=\"tight\", dpi=200)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function spMapprox(S, A, alpha; ord=1, tol_eigvals_sep=1e-5)\n",
    "    dim = size(A)[1]\n",
    "    mus, eigvecs = LA.eigen(im*A)  # im*A is hermitian so Julia calls a specialized solver which returns unitary eigvecs\n",
    "    eigvals = im .* mus\n",
    "    mus_sep = minimum(sort(mus)[i+1] - sort(mus)[i] for i=1:length(mus)-1)\n",
    "    @assert mus_sep > tol_eigvals_sep \"the eigenvalues of A are not simple, not implemented\"\n",
    "\n",
    "    spM01 = [ eigvals[i] + alpha * eigvecs[:,i]' * S * eigvecs[:,i]    for i=1:dim]\n",
    "    if ord == 1\n",
    "        return spM01\n",
    "    elseif ord == 2\n",
    "        spM2 = zeros(Complex, dim)\n",
    "        for i=1:dim, j=1:dim\n",
    "            if i != j\n",
    "                spM2[i] += alpha^2 * abs(eigvecs[:,i]' * S * eigvecs[:,j])^2 / (eigvals[i] - eigvals[j])\n",
    "            end\n",
    "        end\n",
    "        return spM01 .+ spM2\n",
    "    else\n",
    "        throw(\"not implemented\")\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Random.seed!(rng, 1234)\n",
    "\n",
    "dimx, dimy = 3, 3  # (need to be equal for the NE to be unique)\n",
    "P = randn(rng, dimx, dimy)\n",
    "alpha_s = 10. .^ (-3:1:0)\n",
    "spM_s       = zeros(Complex, dimx+dimy, length(alpha_s))\n",
    "spMapprox_s = zeros(Complex, dimx+dimy, length(alpha_s))\n",
    "\n",
    "A = zeros(dimx+dimy, dimx+dimy)\n",
    "A[1:dimx, dimx+1:end] = P\n",
    "A[dimx+1:end, 1:dimx] = -P'\n",
    "S1 = zeros(dimx+dimy, dimx+dimy)\n",
    "S1[1,1] = 1\n",
    "for (i, alpha) in enumerate(alpha_s)\n",
    "    spM_s[:, i] = LA.eigvals(A .+ alpha*S1)\n",
    "    spMapprox_s[:, i] = spMapprox(S1, A, alpha; ord=2)\n",
    "end\n",
    "\n",
    "## figure out xmin and xmax\n",
    "maxIm = maximum(abs.(imag.(vcat(spM_s, spMapprox_s))))\n",
    "ymin, ymax = maxIm .* (-1.3, 1.3)\n",
    "xmin, xmax = minimum(real.(vcat(spM_s, spMapprox_s))) / 2,  maximum(real.(vcat(spM_s, spMapprox_s))) * 40"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "markers = [\"o\", \"D\", \"P\", \"X\", \"s\"]\n",
    "figure(figsize=[5,3])\n",
    "plot([xmin,xmax], [0, 0], label=\"\", color=:red)\n",
    "for (i, alpha) in enumerate(alpha_s)\n",
    "    semilogx(real.(spMapprox_s[:,i]), imag.(spMapprox_s[:,i]), markersize=11, lw=3,\n",
    "        markers[i], color=:gray, alpha=0.7)\n",
    "    semilogx(real.(spM_s[:,i]), imag.(spM_s[:,i]), markersize=7, lw=3,\n",
    "        label=(i == 1 ? L\"\\alpha=10^{%$(Integer(log10(alpha)))}\" : L\"~~~~~~10^{%$(Integer(log10(alpha)))}\"), \n",
    "        # label=L\"10^{%$(Integer(log10(alpha)))}\", \n",
    "        markers[i], color=:blue, alpha=0.7)\n",
    "end\n",
    "legend()\n",
    "xlabel(L\"\\Re(\\lambda)\")\n",
    "ylabel(L\"\\Im(\\lambda)\")\n",
    "xlim([xmin, xmax])\n",
    "ylim([ymin, ymax])\n",
    "grid(\"on\")\n",
    "savefig(\"reg_bilin_spM__dim$(dimx)$(dimy).png\", bbox_inches=\"tight\", dpi=200)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.3",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.3"
  },
  "orig_nbformat": 4,
  "vscode": {
   "interpreter": {
    "hash": "6dd6fd9a984b8e9f8b632a0ce0a6e605415975433a0eb1572562d03f135e73a0"
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
